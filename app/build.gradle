apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "me.bakuon.demo.flavorsdemo"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            storeFile file("key")
            storePassword "123456"
            keyAlias "demo"
            keyPassword "123456"
        }
    }
    buildTypes {
        debug {
//            minifyEnabled true
//            // Zipalign优化
//            zipAlignEnabled true
//            // 移除无用的resource文件
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 签名
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled true
            // Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // 签名
            signingConfig signingConfigs.release
        }
    }
    // flavor 分类名
    flavorDimensions "env"
    productFlavors {
        // 开发环境
        develop{
            // buildConfig 类添加成员变量并赋值
            buildConfigField "int", "ENV_TYPE", "1"
            // 修改包名
            applicationId "me.bakuon.demo.flavorsdemo"
            // 替换 manifest 中的占位符
            manifestPlaceholders = [
                    app_name: "开发-FlavorsDemo"
            ]
            // 分类
            // gradle 插件 3.0.0 以后版本必须，老版本可以不用
            dimension "env"
        }
        // 预上线环境
        preRelease{
            buildConfigField "int", "ENV_TYPE", "2"
            manifestPlaceholders = [
                    app_name: "预-FlavorsDemo"
            ]
            dimension "env"
        }
        // 生产环境
        product{
            buildConfigField "int", "ENV_TYPE", "3"
            manifestPlaceholders = [
                    app_name: "FlavorsDemo"
            ]
            dimension "env"
        }
    }
    // 自定义 apk 文件名
    // 老版 gradle 插件
//    android.applicationVariants.all { variant ->
//        variant.outputs.each { output ->
//            def outputFile = output.outputFile
//            if (outputFile != null && outputFile.name.endsWith('.apk')) {
//                //这里修改apk文件名
//                def fileName = "FlavorsDemo-${variant.baseName}-${variant.versionCode}-V${variant.versionName}-${new Date().format("yyyy_MM_dd_HH_mm", TimeZone.getDefault())}.apk"
//                output.outputFile = new File(outputFile.parent, fileName)
//            }
//        }
//    }
    // gradle 插件 3.0.0 以后版本
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "FlavorsDemo-${variant.baseName}-${variant.versionCode}-V${variant.versionName}-${new Date().format("yyyy_MM_dd_HH_mm", TimeZone.getDefault())}.apk"
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test:runner:1.0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:3.0.1'
}
